; Script initially generated by the HM NIS Edit Script Wizard.
; Copyright Jeremy Sanders 2007
; $Id: veusz.nsi 607 2007-05-20 10:23:15Z jeremysanders $
; This is for output generated by pyinstaller

; Change this to specify location to build installer from
!define VEUSZ_SRC_DIR "c:\build\veusz"
!define PYINST_DIR "${VEUSZ_SRC_DIR}\dist\veusz_main"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Veusz"
; !define PRODUCT_VERSION "1.X.Y"
!define PRODUCT_PUBLISHER "Jeremy Sanders"
!define PRODUCT_WEB_SITE "https://veusz.github.io/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\veusz.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

SetCompressor /solid lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "${PYINST_DIR}\COPYING"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\veusz.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION ShowReadme
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "veusz-${PRODUCT_VERSION}-windows-setup.exe"
InstallDir "$PROGRAMFILES\Veusz"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

; taken from http://stackoverflow.com/questions/719631/how-do-i-require-user-to-uninstall-previous-version-with-nsis
; The "" makes the section hidden.
Section "" SecUninstallPrevious
    Call UninstallPrevious
SectionEnd

Function UninstallPrevious

    ; Check for uninstaller.
    ReadRegStr $R0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
    ${If} $R0 == ""
        Goto Done
    ${EndIf}

    DetailPrint "Removing previous installation."

    ; Run the uninstaller
    ExecWait '"$R0" _?=$INSTDIR'

    Done:

FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite try
  File "${PYINST_DIR}\*.exe"
  File "${PYINST_DIR}\*.dll"
  File "${PYINST_DIR}\*.pyd"
  File "${PYINST_DIR}\*.zip"
  File "${PYINST_DIR}\*.manifest"
  File "${VEUSZ_SRC_DIR}\veusz\embed.py"
  File "${VEUSZ_SRC_DIR}\veusz\__init__.py"
  File /oname=qt.conf "${VEUSZ_SRC_DIR}\support\qt-windows-pyinstaller.conf"

  CreateDirectory "$SMPROGRAMS\Veusz"
  CreateShortCut "$SMPROGRAMS\Veusz\Veusz.lnk" "$INSTDIR\veusz.exe"
  CreateShortCut "$DESKTOP\Veusz.lnk" "$INSTDIR\veusz.exe"
  SetOverwrite ifnewer

  File "${PYINST_DIR}\README"
  File "${PYINST_DIR}\COPYING"
  File "${PYINST_DIR}\VERSION"

  SetOutPath "$INSTDIR\examples"
  File "${PYINST_DIR}\examples\*.vsz"
  File "${PYINST_DIR}\examples\*.py"
  File "${PYINST_DIR}\examples\*.dat"
  File "${PYINST_DIR}\examples\*.csv"

  SetOutPath "$INSTDIR\h5py"
  File "${PYINST_DIR}\h5py\*.pyd"

  SetOutPath "$INSTDIR\icons"
  File "${PYINST_DIR}\icons\*.png"
  File "${PYINST_DIR}\icons\*.ico"
  File "${PYINST_DIR}\icons\*.svg"

  SetOutPath "$INSTDIR\iminuit"
  File "${PYINST_DIR}\iminuit\*.pyd"

  SetOutPath "$INSTDIR\numpy\core"
  File "${PYINST_DIR}\numpy\core\*.pyd"
  SetOutPath "$INSTDIR\numpy\fft"
  File "${PYINST_DIR}\numpy\fft\*.pyd"
  SetOutPath "$INSTDIR\numpy\linalg"
  File "${PYINST_DIR}\numpy\linalg\*.pyd"
  SetOutPath "$INSTDIR\numpy\random"
  File "${PYINST_DIR}\numpy\random\*.pyd"

  SetOutPath "$INSTDIR\PyQt5"
  File "${PYINST_DIR}\PyQt5\*.pyd"
  SetOutPath "$INSTDIR\PyQt5\Qt\translations"
  File "${PYINST_DIR}\PyQt5\Qt\translations\*.qm"
  SetOutPath "$INSTDIR\PyQt5\Qt\plugins\iconengines"
  File "${PYINST_DIR}\PyQt5\Qt\plugins\iconengines\*.dll"
  SetOutPath "$INSTDIR\PyQt5\Qt\plugins\imageformats"
  File "${PYINST_DIR}\PyQt5\Qt\plugins\imageformats\*.dll"
  SetOutPath "$INSTDIR\PyQt5\Qt\plugins\platforms"
  File "${PYINST_DIR}\PyQt5\Qt\plugins\platforms\*.dll"
  SetOutPath "$INSTDIR\PyQt5\Qt\plugins\printsupport"
  File "${PYINST_DIR}\PyQt5\Qt\plugins\printsupport\*.dll"

  SetOutPath "$INSTDIR\ui"
  File "${PYINST_DIR}\ui\*.ui"

  SetOutPath "$INSTDIR\veusz\helpers"
  File "${PYINST_DIR}\veusz\helpers\*.pyd"

  SetOutPath "$INSTDIR\win32com\shell"
  File "${PYINST_DIR}\win32com\shell\*.pyd"

  SetOutPath "$INSTDIR"

  WriteRegStr HKCR ".vsz" "" "Veusz.Document"
  WriteRegStr HKCR "Veusz.Document" "" "Veusz document"
  WriteRegStr HKCR "Veusz.Document\shell\open\command" "" '"$INSTDIR\veusz.exe" "%1"'
  WriteRegStr HKCR "Veusz.Document\DefaultIcon" "" '"$INSTDIR\icons\veusz.ico"'
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Veusz\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Veusz\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Function ShowReadme
  Exec "notepad.exe $INSTDIR\README"
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

  Delete "$INSTDIR\COPYING"
  Delete "$INSTDIR\README"
  Delete "$INSTDIR\VERSION"

  Delete "$INSTDIR\*.pyd"
  Delete "$INSTDIR\*.dll"
  Delete "$INSTDIR\*.exe"
  Delete "$INSTDIR\*.manifest"
  Delete "$INSTDIR\*.zip"
  Delete "$INSTDIR\embed.py"
  Delete "$INSTDIR\__init__.py"
  Delete "$INSTDIR\qt.conf"

  Delete "$INSTDIR\veusz\helpers\*.pyd"
  Delete "$INSTDIR\win32com\shell\*.pyd"
  Delete "$INSTDIR\iminuit\*.pyd"
  Delete "$INSTDIR\PyQt5\*.pyd"
  Delete "$INSTDIR\numpy\core\*.pyd"
  Delete "$INSTDIR\numpy\fft\*.pyd"
  Delete "$INSTDIR\numpy\linalg\*.pyd"
  Delete "$INSTDIR\numpy\random\*.pyd"
  Delete "$INSTDIR\h5py\*.pyd"

  Delete "$INSTDIR\PyQt5\Qt\plugins\iconengines\*.dll"
  Delete "$INSTDIR\PyQt5\Qt\plugins\imageformats\*.dll"
  Delete "$INSTDIR\PyQt5\Qt\plugins\platforms\*.dll"
  Delete "$INSTDIR\PyQt5\Qt\plugins\printsupport\*.dll"
  Delete "$INSTDIR\PyQt5\Qt\translations\*.qm"

  Delete "$INSTDIR\icons\*.png"
  Delete "$INSTDIR\icons\*.ico"
  Delete "$INSTDIR\icons\*.svg"
  Delete "$INSTDIR\ui\*.ui"
  Delete "$INSTDIR\examples\*.*"

  Delete "$SMPROGRAMS\Veusz\Uninstall.lnk"
  Delete "$SMPROGRAMS\Veusz\Website.lnk"
  Delete "$DESKTOP\Veusz.lnk"
  Delete "$SMPROGRAMS\Veusz\Veusz.lnk"

  RMDir "$SMPROGRAMS\Veusz"
  RMDir "$INSTDIR\examples"
  RMDir "$INSTDIR\h5py"
  RMDir "$INSTDIR\icons"
  RMDir "$INSTDIR\iminuit"
  RMDir "$INSTDIR\numpy\core"
  RMDir "$INSTDIR\numpy\fft"
  RMDir "$INSTDIR\numpy\linalg"
  RMDir "$INSTDIR\numpy\random"
  RMDir "$INSTDIR\numpy"
  RMDir "$INSTDIR\PyQt5\Qt\plugins\iconengines"
  RMDir "$INSTDIR\PyQt5\Qt\plugins\imageformats"
  RMDir "$INSTDIR\PyQt5\Qt\plugins\platforms"
  RMDir "$INSTDIR\PyQt5\Qt\plugins\printsupport"
  RMDir "$INSTDIR\PyQt5\Qt\plugins"
  RMDir "$INSTDIR\PyQt5\Qt\translations"
  RMDir "$INSTDIR\PyQt5\Qt"
  RMDir "$INSTDIR\PyQt5"
  RMDir "$INSTDIR\ui"
  RMDir "$INSTDIR\veusz\helpers"
  RMDir "$INSTDIR\veusz"
  RMDir "$INSTDIR\win32com\shell"
  RMDir "$INSTDIR\win32com"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCR ".vsz"
  DeleteRegKey HKCR "Veusz.Document\shell\open\command"
  DeleteRegKey HKCR "Veusz.Document\DefaultIcon"
  DeleteRegKey HKCR "Veusz.Document"
  SetAutoClose true
SectionEnd
