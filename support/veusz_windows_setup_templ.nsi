; Script initially generated by the HM NIS Edit Script Wizard.
; Copyright Jeremy Sanders 2007
; $Id: veusz.nsi 607 2007-05-20 10:23:15Z jeremysanders $
; This is for output generated by pyinstaller

; Change this to specify location to build installer from
!define VEUSZ_SRC_DIR "@@VEUSZ_SRC_DIR@@"
!define PYINST_DIR "@@VEUSZ_DIST_DIR@@"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Veusz"
!define PRODUCT_VERSION "@@PRODUCT_VERSION@@"
!define PRODUCT_PUBLISHER "Jeremy Sanders"
!define PRODUCT_WEB_SITE "https://veusz.github.io/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\veusz.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

SetCompressor /solid lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "${PYINST_DIR}\COPYING"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\veusz.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README.md"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION ShowReadme
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

!include "x64.nsh"

Function .onInit
  ${IfNot} ${RunningX64}
    MessageBox MB_OK|MB_ICONINFORMATION "This installer is for 64 bit (x64) Windows only. Exiting."
    Abort
  ${EndIf}
  SetRegView 64
FunctionEnd

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "installer_out\veusz-${PRODUCT_VERSION}-x64-windows-setup.exe"
InstallDir "$PROGRAMFILES64\Veusz"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

; taken from http://stackoverflow.com/questions/719631/how-do-i-require-user-to-uninstall-previous-version-with-nsis
; The "" makes the section hidden.
Section "" SecUninstallPrevious
    Call UninstallPrevious
SectionEnd

Function UninstallPrevious
    SetRegView 32

    ; Check for uninstaller.
    ReadRegStr $R0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
    ${If} $R0 == ""
        Goto Next
    ${EndIf}

    DetailPrint "Removing previous installation (32 bit)."

    ; Run the uninstaller
    ExecWait '"$R0" _?=$INSTDIR'

    Next:
    SetRegView 64

    ; Check for uninstaller.
    ReadRegStr $R0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
    ${If} $R0 == ""
        Goto Done
    ${EndIf}

    DetailPrint "Removing previous installation (64 bit)."

    ; Run the uninstaller
    ExecWait '"$R0" _?=$INSTDIR'

    Done:

FunctionEnd

Section "MainSection" SEC01
  SetRegView 64

  SetOverwrite try

; replaced by commands to copy files
@@COPY_FILES@@

; install shortcut
  CreateDirectory "$SMPROGRAMS\Veusz"
  CreateShortCut "$SMPROGRAMS\Veusz\Veusz.lnk" "$INSTDIR\veusz.exe"
  CreateShortCut "$DESKTOP\Veusz.lnk" "$INSTDIR\veusz.exe"

; setup associations
  WriteRegStr HKCR ".vsz" "" "Veusz.Document"
  WriteRegStr HKCR "Veusz.Document" "" "Veusz document"
  WriteRegStr HKCR "Veusz.Document\shell\open\command" "" '"$INSTDIR\veusz.exe" "%1"'
  WriteRegStr HKCR "Veusz.Document\DefaultIcon" "" '"$INSTDIR\icons\veusz.ico"'
SectionEnd

Section -AdditionalIcons
  SetRegView 64

  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Veusz\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Veusz\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  SetRegView 64

  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Function ShowReadme
  Exec "notepad.exe $INSTDIR\README.md"
FunctionEnd

Section Uninstall
  SetRegView 64

  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

@@DELETE_FILES@@

  Delete "$SMPROGRAMS\Veusz\Uninstall.lnk"
  Delete "$SMPROGRAMS\Veusz\Website.lnk"
  Delete "$DESKTOP\Veusz.lnk"
  Delete "$SMPROGRAMS\Veusz\Veusz.lnk"
  RMDir "$SMPROGRAMS\Veusz"

  ; clean up registry
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCR ".vsz"
  DeleteRegKey HKCR "Veusz.Document\shell\open\command"
  DeleteRegKey HKCR "Veusz.Document\DefaultIcon"
  DeleteRegKey HKCR "Veusz.Document"
  SetAutoClose true
SectionEnd
