; Script initially generated by the HM NIS Edit Script Wizard.
; Copyright Jeremy Sanders 2007
; $Id: veusz.nsi 607 2007-05-20 10:23:15Z jeremysanders $
; This is for output generated by pyinstaller

; Change this to specify location to build installer from
!define PYINST_DIR "c:\source\veusz\distveusz_main"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Veusz"
!define PRODUCT_VERSION "1.4"
!define PRODUCT_PUBLISHER "Jeremy Sanders"
!define PRODUCT_WEB_SITE "http://home.gna.org/veusz/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\veusz.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

SetCompressor /solid lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "${PYINST_DIR}\COPYING"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\veusz.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\Veusz"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite try
  File "${PYINST_DIR}\*.exe"
  File "${PYINST_DIR}\*.dll"
  File "${PYINST_DIR}\*.pyd"

  CreateDirectory "$SMPROGRAMS\Veusz"
  CreateShortCut "$SMPROGRAMS\Veusz\Veusz.lnk" "$INSTDIR\veusz.exe"
  CreateShortCut "$DESKTOP\Veusz.lnk" "$INSTDIR\veusz.exe"
  SetOverwrite ifnewer

  File "${PYINST_DIR}\README"
  File "${PYINST_DIR}\COPYING"
  File "${PYINST_DIR}\VERSION"

  SetOutPath "$INSTDIR\windows\icons"
  File "${PYINST_DIR}\windows\icons\*.png"
  File "${PYINST_DIR}\windows\icons\*.ico"
  File "${PYINST_DIR}\windows\icons\*.svg"
  
  SetOutPath "$INSTDIR\widgets\data"
  File "${PYINST_DIR}\widgets\data\*.dat"
  
  SetOutPath "$INSTDIR\dialogs"
  File "${PYINST_DIR}\dialogs\*.ui"
  
  SetOutPath "$INSTDIR\examples"
  File "${PYINST_DIR}\examples\*.vsz"
  File "${PYINST_DIR}\examples\*.py"
  File "${PYINST_DIR}\examples\*.dat"
  File "${PYINST_DIR}\examples\*.csv"
  SetOutPath "$INSTDIR"

  SetOutPath "$INSTDIR\iconengines"
  File "${PYINST_DIR}\iconengines\*.dll"
  SetOutPath "$INSTDIR"

  SetOutPath "$INSTDIR\imageformats"
  File "${PYINST_DIR}\imageformats\*.dll"
  SetOutPath "$INSTDIR"

  WriteRegStr HKCR ".vsz" "" "Veusz.Document"
  WriteRegStr HKCR "Veusz.Document" "" "Veusz document"
  WriteRegStr HKCR "Veusz.Document\shell\open\command" "" '"$INSTDIR\veusz.exe" "%1"'
  WriteRegStr HKCR "Veusz.Document\DefaultIcon" "" '"$INSTDIR\windows\icons\veusz.ico"'
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Veusz\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Veusz\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\veusz.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

  Delete "$INSTDIR\COPYING"
  Delete "$INSTDIR\README"
  Delete "$INSTDIR\VERSION"

  Delete "$INSTDIR\*.pyd"
  Delete "$INSTDIR\*.dll"
  Delete "$INSTDIR\*.exe"
  Delete "$INSTDIR\*.zip"

  Delete "$INSTDIR\iconengines\*.dll"
  Delete "$INSTDIR\imageformats\*.dll"

  Delete "$INSTDIR\windows\icons\*.png"
  Delete "$INSTDIR\windows\icons\*.ico"
  Delete "$INSTDIR\windows\icons\*.svg"
  Delete "$INSTDIR\widgets\data\*.dat"
  Delete "$INSTDIR\dialogs\*.ui"
  Delete "$INSTDIR\examples\*.*"

  Delete "$SMPROGRAMS\Veusz\Uninstall.lnk"
  Delete "$SMPROGRAMS\Veusz\Website.lnk"
  Delete "$DESKTOP\Veusz.lnk"
  Delete "$SMPROGRAMS\Veusz\Veusz.lnk"

  RMDir "$SMPROGRAMS\Veusz"
  RMDir "$INSTDIR\iconengines"
  RMDIR "$INSTDIR\imageformats"
  RMDir "$INSTDIR\windows\icons"
  RMDIR "$INSTDIR\windows"
  RMDir "$INSTDIR\widgets\data"
  RMDIR "$INSTDIR\widgets"
  RMDir "$INSTDIR\dialogs"
  RMDIR "$INSTDIR\examples"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCR ".vsz"
  DeleteRegKey HKCR "Veusz.Document\shell\open\command"
  DeleteRegKey HKCR "Veusz.Document\DefaultIcon"
  DeleteRegKey HKCR "Veusz.Document"
  SetAutoClose true
SectionEnd
